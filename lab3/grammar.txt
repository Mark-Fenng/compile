separator
:
grammar
program:translation_unit
primary_expression:IDENTIFIER
primary_expression:CONSTANT
primary_expression:L_PAREN expression R_PAREN

postfix_expression:primary_expression
postfix_expression:postfix_expression L_BRACK expression R_BRACK
postfix_expression:postfix_expression L_PAREN R_PAREN
postfix_expression:postfix_expression L_PAREN argument_expression_list R_PAREN

unary_expression:postfix_expression
unary_expression:NOT postfix_expression
unary_expression:SUB postfix_expression

multiplicative_expression:unary_expression
multiplicative_expression:multiplicative_expression MUL postfix_expression
multiplicative_expression:multiplicative_expression DIV postfix_expression
multiplicative_expression:multiplicative_expression MOD postfix_expression

additive_expression:multiplicative_expression
additive_expression:additive_expression ADD multiplicative_expression
additive_expression:additive_expression SUB multiplicative_expression

relational_expression:additive_expression
relational_expression:relational_expression LT additive_expression
relational_expression:relational_expression GT additive_expression
relational_expression:relational_expression LE additive_expression
relational_expression:relational_expression GE additive_expression

equality_expression:relational_expression
equality_expression:equality_expression EQ relational_expression
equality_expression:equality_expression NEQ relational_expression

logical_and_expression:equality_expression
logical_and_expression:logical_and_expression AND equality_expression

logical_or_expression:logical_and_expression
logical_or_expression:logical_or_expression OR logical_and_expression

assignment_expression:logical_or_expression
assignment_expression:postfix_expression assignment_operator assignment_expression

argument_expression_list:assignment_expression
argument_expression_list:argument_expression_list COMMA assignment_expression

assignment_operator:ASSIGN
assignment_operator:MUL_ASSIGN
assignment_operator:DIV_ASSIGN
assignment_operator:MOD_ASSIGN
assignment_operator:ADD_ASSIGN
assignment_operator:SUB_ASSIGN
assignment_operator:AND_ASSIGN
assignment_operator:OR_ASSIGN

expression:assignment_expression
expression:expression COMMA assignment_expression

declaration:declaration_specifiers init_declarator_list SEMICOLON

declaration_specifiers:VAR
declaration_specifiers:VAL

init_declarator_list:init_declarator
init_declarator_list:init_declarator_list COMMA init_declarator

init_declarator:IDENTIFIER
init_declarator:IDENTIFIER ASSIGN initializer

identifier_list:IDENTIFIER
identifier_list:identifier_list COMMA IDENTIFIER

initializer:assignment_expression
initializer:L_CURLY initializer_list L_CURLY

initializer_list:initializer
initializer_list:initializer_list COMMA initializer

statement:compound_statement
statement:expression_statement
statement:selection_statement
statement:iteration_statement
statement:jump_statement

compound_statement:L_CURLY R_CURLY
compound_statement:L_CURLY statement_list R_CURLY
compound_statement:L_CURLY declaration_list R_CURLY
compound_statement:L_CURLY declaration_list statement_list R_CURLY

declaration_list:declaration
declaration_list:declaration_list declaration

statement_list:statement
statement_list:statement_list statement

expression_statement:SEMICOLON
expression_statement:expression SEMICOLON

elif_statement:ELIF L_PAREN expression R_PAREN compound_statement
elif_statement:elif_statement ELIF L_PAREN expression R_PAREN compound_statement

selection_statement:IF L_PAREN expression R_PAREN compound_statement
selection_statement:IF L_PAREN expression R_PAREN compound_statement elif_statement
selection_statement:IF L_PAREN expression R_PAREN compound_statement ELSE compound_statement
selection_statement:IF L_PAREN expression R_PAREN compound_statement elif_statement ELSE compound_statement

iteration_statement:WHILE L_PAREN expression R_PAREN statement
iteration_statement:FOR L_PAREN expression_statement expression_statement R_PAREN statement
iteration_statement:FOR L_PAREN expression_statement expression_statement expression R_PAREN statement

jump_statement:CONTINUE SEMICOLON
jump_statement:BREAK SEMICOLON
jump_statement:RETURN SEMICOLON
jump_statement:RETURN expression SEMICOLON

translation_unit:external_declaration
translation_unit:translation_unit external_declaration

external_declaration:function_definition
external_declaration:declaration
external_declaration:expression_statement
external_declaration:selection_statement
external_declaration:iteration_statement

function_definition:FUNC IDENTIFIER L_PAREN R_PAREN compound_statement
function_definition:FUNC IDENTIFIER L_PAREN identifier_list R_PAREN compound_statement